apply plugin: 'io.spring.dependency-management'

repositories {
    jcenter()
}

dependencyManagement {
    
    imports {
        mavenBom 'org.springframework:spring-framework-bom:4.3.8.RELEASE'
        mavenBom 'com.google.inject:guice-bom:4.1.0'
    }

    dependencies {

        dependencySet(group: 'ch.qos.logback', version: '1.1.7') {
            entry 'logback-classic'
            entry 'logback-core'
        }

        dependencySet(group: 'com.fasterxml.jackson.core', version: '2.8.8') {
            entry 'jackson-annotations'
            entry 'jackson-core'
            entry 'jackson-databind'
        }

        dependency 'com.github.javaparser:javaparser-core:3.0.0-alpha.10'

        dependency 'com.google.auto.service:auto-service:1.0-rc2'

        dependency 'com.google.code.findbugs:jsr305:3.0.1'

        dependency 'com.google.guava:guava:21.0'

        dependency 'com.google.testing.compile:compile-testing:0.10'

        dependency 'com.google.truth:truth:0.30'

        dependency 'com.squareup:javapoet:1.8.0'

        dependency 'javax.servlet:javax.servlet-api:3.1.0'

        dependency('junit:junit:4.12')

        dependency 'org.codehaus.groovy:groovy-all:2.4.10'

        dependencySet(group: 'org.hamcrest', version: '1.3') {
            entry 'hamcrest-core'
            entry 'hamcrest-library'
        }

        dependencySet(group: 'org.slf4j', version: '1.7.25') {
            entry 'slf4j-api'
            entry 'jcl-over-slf4j'
        }

        dependencySet(group: 'org.spockframework', version: '1.1-groovy-2.4-rc-3') {
            entry 'spock-core'
        }
    }
}



DependencySubstitutions.metaClass.managedModule { String spec ->
    def managedVersion = dependencyManagement.managedVersions[spec]
    return delegate.module("$spec:$managedVersion")
}


configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('commons-logging:commons-logging') with managedModule('org.slf4j:jcl-over-slf4j')
    }
}
